// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    apply from: 'dependencies.gradle'
    apply from: 'versions.gradle'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "$deps.gradle"
        classpath "$deps.gradle_bintray"
        classpath "$deps.kotlin_gradle_plugin"
        classpath "$deps.licenses"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion 29
                buildToolsVersion '29.0.3'

                defaultConfig {
                    minSdkVersion 14
                    targetSdkVersion 29
                    versionCode 1
                    versionName '0.1'
                    testInstrumentationRunner "$deps.android_junit_runner"
                    consumerProguardFiles 'consumer-rules.pro'
                    multiDexEnabled true
                    vectorDrawables.useSupportLibrary = true
                }

                buildTypes {
                    release {
                        minifyEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }
            }
        }
    }
}

afterEvaluate { project ->
    if (project.hasProperty('android')) {
        project.android.libraryVariants.each {
            tasks."license${it.name.capitalize()}Report"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
